buildscript {
    ext{
        creds = {
            username = findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://maven.pkg.github.com/trevorism/gradle-acceptance-plugin")
            credentials creds
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.5.0'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.3.1'
    }
}

plugins {
    id "groovy"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.3.4"
    id "jacoco"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"

repositories {
    mavenCentral()
    maven {
        url uri("https://maven.pkg.github.com/trevorism/secure-http-utils")
        credentials creds
    }
}

dependencies {
    annotationProcessor("io.micronaut.validation:micronaut-validation-processor")

    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.security:micronaut-security")
    runtimeOnly("org.yaml:snakeyaml")

    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation 'io.projectreactor:reactor-core:3.6.4'

    implementation("io.swagger.core.v3:swagger-annotations")
    compileOnly("io.micronaut.openapi:micronaut-openapi"){
        exclude module:"slf4j-nop"
    }

    implementation 'com.trevorism:micronaut-security-utils:1.1.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.cloud:google-cloud-datastore:2.18.6'

    acceptanceImplementation 'io.cucumber:cucumber-groovy:6.10.4'
    acceptanceImplementation 'com.trevorism:secure-http-utils:2.1.0'
    acceptanceImplementation 'com.google.code.gson:gson:2.10.1'
}

application {
    mainClass.set("com.trevorism.Application")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.trevorism.*")
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
    groovyOptions.forkOptions.jvmArgs.add('-Dmicronaut.openapi.views.spec=rapidoc.enabled=false,swagger-ui.enabled=true,swagger-ui.theme=flattop')
}

appengine {
    stage.artifact = layout.buildDirectory.file("libs/${project.name}-all.jar")
    deploy {
        projectId = "trevorism-data"
        version = "2-3-0"
        stopPreviousVersion = true
        promote = true
    }
}